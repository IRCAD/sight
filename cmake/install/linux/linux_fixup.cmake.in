cmake_policy(SET CMP0011 NEW)
cmake_policy(SET CMP0012 NEW)
cmake_policy(SET CMP0053 NEW)

include(@FWCMAKE_INSTALL_FILES_DIR@/helper.cmake)

# Run the script when installing but not a second time when running CPack
if ( NOT ${CMAKE_INSTALL_PREFIX} MATCHES "/_CPack_Packages/.*/(TGZ|ZIP)/")

    #Find all /*/.so* in /Modules /plugins and /vlc/plugins
    file(GLOB_RECURSE MODULES FOLLOW_SYMLINKS "@CMAKE_INSTALL_PREFIX@/@SIGHT_MODULE_LIB_PREFIX@/*@CMAKE_SHARED_LIBRARY_SUFFIX@*")
    file(GLOB_RECURSE PLUGINS FOLLOW_SYMLINKS
        "@CMAKE_INSTALL_PREFIX@/plugins/*@CMAKE_SHARED_LIBRARY_SUFFIX@*"
        "@CMAKE_INSTALL_PREFIX@/vlc/plugins/*@CMAKE_SHARED_LIBRARY_SUFFIX@*"
    )

    include(BundleUtilities)
    set(LIBS "")
    list(APPEND LIBS ${MODULES} ${PLUGINS} @QT_PLUGINS@)
    # Also add modules from the SDK
    if(@FW_BUILD_EXTERNAL@)

        foreach(REQUIREMENT @PROJECT_REQUIREMENTS@)
            file(GLOB_RECURSE MODULES FOLLOW_SYMLINKS "@Sight_LIBRARY_DIR@/${REQUIREMENT}*/*@CMAKE_SHARED_LIBRARY_SUFFIX@*")
            if(MODULES)
                list(APPEND LIBS ${MODULES})
            endif()
        endforeach()

    endif()

    set(USE_SYSTEM_LIB @USE_SYSTEM_LIB@)
    if(NOT USE_SYSTEM_LIB)
        #Look for all directories containing '.so*' files
        findLibsAndModules(LIBS_MODULES_DIRECTORIES)

        list(APPEND LIBS_MODULES_DIRECTORIES @FW_EXTERNAL_LIBRARIES_DIR@)
        list(APPEND LIBS_MODULES_DIRECTORIES @CMAKE_INSTALL_PREFIX@/lib)

        if(@FW_BUILD_EXTERNAL@)
            list(APPEND LIBS_MODULES_DIRECTORIES @Sight_LIBRARY_DIR@)
        endif()
        set(LAUNCHER_PATH @CMAKE_INSTALL_PREFIX@/@LAUNCHER_PATH@)

        # We need to copy first the libraries as stated by the BundleUtilities documentation :
        # "If you pass plugins to fixup_bundle as the libs parameter, you should install them or
        #  copy them into the module before calling fixup_bundle. The <libs> parameter is a list
        #  of libraries that must be fixed up, but that cannot be determined by otool output
        #  analysis (i.e. plugins)."
        foreach(LIB ${LIBS})
            file(COPY ${LIB} DESTINATION "@CMAKE_INSTALL_PREFIX@/bin/")
        endforeach()

        #Call fixup bundle on the fwlauncher and on all 'plugins' libraries (like bundle, plugins and share)
        fixup_bundle("${LAUNCHER_PATH}" "${LIBS}" "${LIBS_MODULES_DIRECTORIES}")
    endif()

    #Move all libraries in 'lib' folder
    #Not needed but much more elegant
    file(GLOB_RECURSE FIXED_LIBS "@CMAKE_INSTALL_PREFIX@/bin/*@CMAKE_SHARED_LIBRARY_SUFFIX@*")

    foreach(LIB ${FIXED_LIBS})
        file(COPY ${LIB} DESTINATION "@CMAKE_INSTALL_PREFIX@/lib/")
        file(REMOVE ${LIB})
    endforeach()

endif()
